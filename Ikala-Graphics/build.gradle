buildscript {
	dependencies {
		classpath "org.yaml:snakeyaml:${snakeYamlVersion}"
	}
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
}

configurations {
	jarInclude
}

import io.franzbecker.gradle.lombok.task.DelombokTask

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

def resourcesDir = sourceSets.main.resources.srcDirs.first()
def config = new org.yaml.snakeyaml.Yaml().load( new File("${resourcesDir}/plugin.yml").newInputStream() )

archivesBaseName = "${config.name}"
version = "${config.version}"

repositories {
    mavenCentral()
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencies {
	implementation project(':Ikala-Core')
	implementation "org.joml:joml:${jomlVersion}"
	implementation "org.lwjgl:lwjgl:${lwjglVersion}"
	implementation "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
	implementation "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
	runtimeOnly "org.lwjgl:lwjgl:${lwjglVersion}:natives-windows"
	runtimeOnly "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-windows"
	runtimeOnly "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-windows"
	implementation "org.l33tlabs.twl:pngdecoder:${pngDecoderVersion}"
	
	jarInclude "org.joml:joml:${jomlVersion}"
	jarInclude "org.lwjgl:lwjgl:${lwjglVersion}"
	jarInclude "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
	jarInclude "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
	jarInclude "org.l33tlabs.twl:pngdecoder:${pngDecoderVersion}"
	jarInclude "org.lwjgl:lwjgl:${lwjglVersion}:natives-windows"
	jarInclude "org.lwjgl:lwjgl-glfw:${lwjglVersion}:natives-windows"
	jarInclude "org.lwjgl:lwjgl-opengl:${lwjglVersion}:natives-windows"

	// Testing
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	
	// lombok
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
	testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

tasks.register('delombok', DelombokTask) {
	description 'Delomboks the source code'
    ext.outputDir = file('build/src-delomboked')
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each { 
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		exceptionFormat 'full'
		events 'passed', 'skipped', 'failed'
		showStandardStreams true
	}
}

tasks.named('javadoc') {
	dependsOn delombok
	source = delombok.outputDir
	failOnError = false
}

tasks.named('clean') {
	delete 'build/src-delomboked'
	// tests generate this
	delete 'logs'
}

tasks.named('jar') {
    from {
        configurations.jarInclude.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
