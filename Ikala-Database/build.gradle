buildscript {
	dependencies {
		classpath "org.yaml:snakeyaml:${snakeYamlVersion}"
	}
}

plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
}

import io.franzbecker.gradle.lombok.task.DelombokTask

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
    withJavadocJar()
}

def resourcesDir = sourceSets.main.resources.srcDirs.first()
def config = new org.yaml.snakeyaml.Yaml().load( new File("${resourcesDir}/plugin.yml").newInputStream() )

archivesBaseName = "${config.name}"
version = "${config.version}"

repositories {
    mavenCentral()
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

dependencies {
	compileOnly project(':Ikala-Core')
	implementation "org.antlr:antlr4:${antlrVersion}"
	implementation "net.bytebuddy:byte-buddy:${byteBuddyVersion}"
	implementation "com.fasterxml:classmate:${classmateVersion}"
	implementation "com.h2database:h2:${h2Version}"
	implementation "org.hibernate.common:hibernate-commons-annotations:${hibernateCommonAnnotationsVersion}"
	implementation "org.hibernate:hibernate-core:${hibernateCoreVersion}"
	implementation "jakarta.xml.bind:jakarta.xml.bind-api:${jakartaXMLBindVersion}"
	implementation "javax.persistence:javax.persistence-api:${javaxPersistenceVersion}"
	implementation "org.glassfish.jaxb:jaxb-runtime:${jaxbVersion}"
	implementation "org.jboss.logging:jboss-logging:${jbossLoggingVersion}"
	implementation "org.jboss.spec.javax.transaction:jboss-transaction-api_1.2_spec:${jbossTransactionVersion}"
	implementation "org.jooq:jooq:${jooqVersion}"
	implementation "org.jooq:jooq-codegen:${jooqVersion}"
	implementation "org.jooq:jooq-meta:${jooqVersion}"
	implementation "javax.transaction:jta:${jtaVersion}"
	implementation "org.reactivestreams:reactive-streams:${reactiveStreamsVersion}"
	implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jSlf4jVersion}"

	// Testing
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	
	// lombok
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
	testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

tasks.register('delombok', DelombokTask) {
	description 'Delomboks the source code'
    ext.outputDir = file('build/src-delomboked')
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each { 
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		exceptionFormat 'full'
		events 'passed', 'skipped', 'failed'
		showStandardStreams true
	}
}

tasks.named('javadoc') {
	dependsOn delombok
	source = delombok.outputDir
	failOnError = false
}

tasks.named('clean') {
	delete 'build/src-delomboked'
	// tests generate this
	delete 'logs'
}
