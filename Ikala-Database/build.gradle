buildscript {
	dependencies {
		classpath "org.yaml:snakeyaml:${snakeYamlVersion}"
	}
}

plugins {
    id 'java'
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'io.franzbecker.gradle-lombok' version '5.0.0'
}

import io.franzbecker.gradle.lombok.task.DelombokTask

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    withSourcesJar()
    withJavadocJar()
}

def resourcesDir = sourceSets.main.resources.srcDirs.first()
def config = new org.yaml.snakeyaml.Yaml().load( new File("${resourcesDir}/plugin.yml").newInputStream() )

archivesBaseName = "${config.name}"
version = "${config.version}"

repositories {
    mavenCentral()
}

eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

configurations {
	provided
	// Make compile extend from our provided configuration so that things added to bundled end up on the compile classpath
	implementation.extendsFrom(provided)
}

dependencies {
	compileOnly project(':Ikala-Core')
	provided "com.h2database:h2:${h2Version}"
	api      "javax.persistence:javax.persistence-api:${javaxPersistenceVersion}"
	provided "javax.persistence:javax.persistence-api:${javaxPersistenceVersion}"
	api      "org.jooq:jooq:${jooqVersion}"
	provided "org.jooq:jooq:${jooqVersion}"
	api      "org.jooq:jooq-codegen:${jooqVersion}"
	provided "org.jooq:jooq-codegen:${jooqVersion}"
	api      "org.jooq:jooq-meta:${jooqVersion}"
	provided "org.jooq:jooq-meta:${jooqVersion}"
	provided "javax.transaction:jta:${jtaVersion}"
	implementation "org.apache.logging.log4j:log4j-slf4j-impl:${log4jSlf4jVersion}"

	// Testing
	testImplementation "org.junit.jupiter:junit-jupiter:${junitVersion}"
	
	// lombok
	compileOnly "org.projectlombok:lombok:${lombokVersion}"
	annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
	testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
	testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

tasks.register('delombok', DelombokTask) {
	description 'Delomboks the source code'
    ext.outputDir = file('build/src-delomboked')
    outputs.dir(outputDir)
    sourceSets.main.java.srcDirs.each { 
        inputs.dir(it)
        args(it, "-d", outputDir)
    }
    doFirst {
        outputDir.deleteDir()
    }
}

tasks.named('test') {
	useJUnitPlatform()
	testLogging {
		exceptionFormat 'full'
		events 'passed', 'skipped', 'failed'
		showStandardStreams true
	}
}

tasks.named('javadoc') {
	dependsOn delombok
	source = delombok.outputDir
	failOnError = false
}

tasks.named('clean') {
	delete 'build/src-delomboked'
	// tests generate this
	delete 'logs'
}

tasks.named('jar') {
    from {
        configurations.provided.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
